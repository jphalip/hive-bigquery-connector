plugins {
    id 'java'
    id 'java-library'
    id 'com.diffplug.spotless' version '6.10.0'
    id 'com.github.johnrengelman.shadow' version '7.1.0'
    id 'maven-publish'
}

group = 'com.google.cloud.hive'
version = '2.1.0-SNAPSHOT'

ext {
    junitJupiterVersion = '5.10.2'
    junitPlatformEngineVersion = '1.10.2'
    junitPlatformLauncherVersion = '1.10.2'
    truthVersion = '1.4.2'

    avroVersion = '1.11.3'
    bigqueryConnectorCommonVersion = '0.37.0'
    gcsConnectorVersion = 'hadoop2-2.2.21'
    googleCloudDataprocVersion = '4.38.0'
    googleCloudStorageVersion = '2.36.1'
    guavaVersion = '33.1.0-jre'
//    hamcrestVersion = '2.2'
    hiverunnerVersion = '6.1.0'
    jacksonVersion = '2.17.0'
    protobufVersion = '3.25.3'
    conscryptOpenjdkUberVersion = '2.5.2'
    slf4jApiVersion = '1.7.36' // Not compatible with version >= 2.0.0
    javaxElVersion = '3.0.0'

    nexusRemoteSkip = false
    javaVersion = '1.8'
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'com.diffplug.spotless'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    spotless {
        java {
            target("src/**/*.java")
            googleJavaFormat()
            removeUnusedImports()
            licenseHeader("""/*
 * Copyright \$YEAR Google Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */""").updateYearWithLatest(false)
        }
    }

    dependencies {
        testImplementation "org.junit.jupiter:junit-jupiter:${junitJupiterVersion}"
        testImplementation("com.google.truth:truth:${truthVersion}") {
            exclude group: 'com.google.guava', module: 'guava'
            exclude group: 'junit', module: 'junit'
        }
//        testImplementation "org.junit.platform:junit-platform-engine:${junitPlatformEngineVersion}"
//        testImplementation "org.junit.platform:junit-platform-launcher:${junitPlatformLauncherVersion}"
    }


//    // TODO: Confirm we need this
//    configurations {
//        compileOnly {
//            extendsFrom implementation
//        }
//    }

    // TODO: Confirm we need this
    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }

    // TODO: Confirm we need this
    tasks.jar {
        manifest {
            attributes(
                    'Implementation-Title': project.name,
                    'Implementation-Version': project.version
            )
        }
    }
}

publishing {
    publications {
        create(MavenPublication) {
            from(components["java"])
            pom {
                url = 'https://github.com/GoogleCloudDataproc/hive-bigquery-connector'
                developers {
                    developer {
                        organization = 'Google Inc.'
                        organizationUrl = 'https://www.google.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:GoogleCloudDataproc/hive-bigquery-connector.git'
                    developerConnection = 'scm:git:git@github.com:GoogleCloudDataproc/hive-bigquery-connector.git'
                    url = 'git@github.com:GoogleCloudDataproc/hive-bigquery-connector.git'
                }
            }
        }
    }
}