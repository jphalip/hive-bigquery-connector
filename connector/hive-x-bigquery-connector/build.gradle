subprojects {

    apply plugin: 'com.github.johnrengelman.shadow'

    dependencies {
        if (System.getProperty("useHive1").toBoolean()) {
            testImplementation "org.datanucleus:datanucleus-api-jdo:3.2.6"
            testImplementation "org.datanucleus:datanucleus-core:3.2.10"
            testImplementation "org.datanucleus:datanucleus-rdbms:3.2.9"
        }
        if (System.getProperty("useHive2").toBoolean() || System.getProperty("useHive3").toBoolean()) {
            testImplementation "org.datanucleus:datanucleus-api-jdo:4.2.4"
            testImplementation "org.datanucleus:datanucleus-core:4.1.17"
            testImplementation "org.datanucleus:datanucleus-rdbms:4.1.19"
        }
    }

    shadowJar {
        archiveClassifier.set('')

        zip64 true

        exclude 'module-info.class'
        exclude 'META-INF/*.SF'
        exclude 'META-INF/*.DSA'
        exclude 'META-INF/*.RSA'
        exclude 'META-INF/MANIFEST.MF'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'

        relocate 'com.google.common', 'hive.integration.repackaged.com.google.common'
        relocate 'com.google.gson', 'hive.integration.repackaged.com.google.gson'
        relocate 'com.google.protobuf', 'hive.integration.repackaged.com.google.protobuf'
        relocate 'io.netty', 'hive.integration.repackaged.io.netty'
        relocate 'org.apache.arrow', 'hive.integration.repackaged.com.org.apache.arrow'

        mergeServiceFiles()

        dependsOn('jar')
    }

    // Create the build properties file ------------------------------------------------------

    tasks.register('createBuildPropertiesFile') {
        doLast {
            String path = "$projectDir/src/main/resources/com/google/cloud/hive/bigquery/connector/utils/"
            File dir = new File(path)
            dir.mkdirs()
            new File("$path/hive-bigquery-connector.properties").withWriter { w ->
                Properties p = new Properties()
                p['connector.version'] = parent.parent.parent.project.version.toString()
                p.each { key, value ->
                    w.write("$key=$value\n")
                }
            }
        }
    }

    processResources {
        dependsOn tasks.named('createBuildPropertiesFile')
    }

    classes {
        dependsOn processResources
    }
}